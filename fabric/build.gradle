import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url = 'https://maven.blamejared.com'
    }
    maven {
        url = "https://maven.terraformersmc.com/"
    }
}

archivesBaseName = archives_base_name

configurations {
    shade
    compile.extendsFrom shade
}

sourceSets {
    main {
        java {
            srcDirs += [
                    rootProject.sourceSets.main.java
            ]
        }
        resources {
            srcDirs += [
                    rootProject.sourceSets.main.resources,
                    "src/generated/resources"
            ]
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.mc_ver}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    //modImplementation('com.epherical.epherolib:EpheroLib-fabric:1.2.0-1.20.6')
    modImplementation files("../jars/EpheroLib-FABRIC-1.2.0-1.21.5.jar")
    modCompileOnly "vazkii.patchouli:Patchouli:1.21-88-FABRIC",{
        exclude(group: "net.fabricmc.fabric-api")
    }

    modCompileOnly "com.terraformersmc:modmenu:${rootProject.modmenu_version}",{
        exclude(group: "net.fabricmc.fabric-api")
    }

    shade(rootProject)
    compileOnly(rootProject)
    implementation(rootProject)
}

loom {
    accessWidenerPath = file("src/main/resources/croptopia.accesswidener")

    runs {
        client {
            client()
            name "Fabric Client"
            ideConfigGenerated = true
        }

        server {
            server()
            name "Fabric Server"
            ideConfigGenerated = true
        }

        datagen {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${getProject().file("build/datagen/output")}"
            runDir "build/datagen"
            ideConfigGenerated = true
        }
        
    }
}

// Preserve the output of data generation in the source tree so we can more easily detect problems
// We could also generate directly into src/generated, but that tends to cause issues with the .cache directory;
// generating into build just seems cleaner.
task copyDatagenOutput(type: Copy) {
    doFirst {
        delete "src/generated/resources"
    }
    from "build/datagen/output"
    into "src/generated/resources"
    exclude ".cache/**"
    
}
runDatagen.finalizedBy copyDatagenOutput


// copy the generated resources from our source tree into the build
processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        // This little horror allows us to have our published modid be 'croptopia-fabric' while still using datagen to 
        // generate resources in the 'croptopia' namespace.  Fabric really doesn't want us to do that, which I guess
        // is sort of understandable, but in our case, given the temporary nature of this codebase, it seems like
        // a reasonable thing to want to do.  This is a little fragile, and it only works because we do runData as a
        // development operation, not as part of the release build.
        def isDatagenBuild = gradle.startParameter.taskNames.contains(":fabric:runData") || gradle.startParameter.taskNames.contains("runData")
        def modId = isDatagenBuild ? "croptopia" : "croptopia-fabric"
        expand "mod_version": rootProject.version, "mod_id": modId
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }

    from(sourceSets.main.resources.srcDirs) {
        include('**/data/**')
        include('**/dependents/**')
        expand "dependent": "c"
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}


tasks.processResources.dependsOn(rootProject.tasks.processResources) // REVIEW 1.21.6 is this necessary?

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    source(rootProject.sourceSets.main.allSource)
    doLast {
        copy {
            from "${buildDir}/resources/main/dependents/platform"
            include "**/**/*.json"
            expand "dependent": "c"
            into "${buildDir}/resources/main/data/c/"
        }
    }
}

shadowJar {
    archiveFileName = rootProject.archives_base_name + "-${rootProject.mc_ver}-FABRIC-dev.jar"
    configurations = [project.configurations.shade]
    relocate 'dependents/platform', 'data/c'
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn tasks.shadowJar
    input = tasks.shadowJar.archiveFile
    archiveFileName = rootProject.archives_base_name + "-${rootProject.mc_ver}-FABRIC-${rootProject.version}.jar"
}

tasks.assemble.dependsOn tasks.remappedShadowJar

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${archives_base_name}-" + artifactId
            //version = version + "-${mc_ver}"
            // add all the jars that should be included when publishing to maven
            //artifact remappedShadowJar
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }

    repositories {
        maven {
            name = "EphericalMaven"
            url = "https://maven.epherical.com/releases"
            credentials {
                username = m_user
                password = m_pass
            }
        }
    }
}
