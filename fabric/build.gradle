import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.modrinth.minotaur" version "2.8.7"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    mavenCentral()
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url = 'https://maven.blamejared.com'
    }
    maven {
        url = "https://maven.terraformersmc.com/"
    }
}

archivesBaseName = archives_base_name

configurations {
    shade
    compile.extendsFrom shade
}

sourceSets {
    main {
        java {
            srcDirs += [
                    rootProject.sourceSets.main.java
            ]
        }
        resources {
            srcDirs += [
                    rootProject.sourceSets.main.resources,
                    "src/generated/resources"
            ]
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    modImplementation files("../jars/${rootProject.epherolib_jar}")
    modCompileOnly "vazkii.patchouli:Patchouli:1.21-88-FABRIC",{
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompileOnly "com.terraformersmc:modmenu:7.2.2",{
        exclude(group: "net.fabricmc.fabric-api")
    }

    shade(rootProject)
    shade files("../jars/${rootProject.epherolib_jar}")
    compileOnly(rootProject)
    implementation(rootProject)
}

loom {
    accessWidenerPath = file("src/main/resources/croptopia.accesswidener")

    runs {
        client {
            client()
            name "Fabric Client"
            ideConfigGenerated = true
        }

        server {
            server()
            name "Fabric Server"
            ideConfigGenerated = true
        }

        datagen {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${getProject().file("build/datagen/output")}"
            runDir "build/datagen"
            ideConfigGenerated = true
        }
    }
}

// Preserve the output of data generation in the source tree so we can more easily detect problems
// We could also generate directly into src/generated, but that tends to cause issues with the .cache directory;
// generating into build just seems cleaner.
task copyDatagenOutput(type: Copy) {
    doFirst {
        delete "src/generated/resources"
    }
    from "build/datagen/output"
    into "src/generated/resources"
    exclude ".cache/**"
}
runDatagen.finalizedBy copyDatagenOutput


// copy the generated resources from our source tree into the build
processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        var templateProperties = rootProject.properties.clone()
        // This little horror allows us to have our published modid be 'croptopia-refabricated' while still using datagen to
        // generate resources in the 'croptopia' namespace.  Fabric really doesn't want us to do that, which I guess
        // is sort of understandable, but in our case, given the temporary nature of this codebase, it seems like
        // a reasonable thing to want to do.  This is a little fragile, and it only works because we do runData as a
        // development operation, not as part of the release build.
        def isDatagenBuild = gradle.startParameter.taskNames.contains(":fabric:runData") || gradle.startParameter.taskNames.contains("runData")
        templateProperties.mod_id = isDatagenBuild ? "croptopia" : "croptopia-refabricated"
        expand(templateProperties)
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }

    from(sourceSets.main.resources.srcDirs) {
        include('**/data/**')
        include('**/dependents/**')
        expand "dependent": "c"
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

tasks.processResources.dependsOn(rootProject.tasks.processResources) // REVIEW 1.21.6 is this necessary?

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    source(rootProject.sourceSets.main.allSource)
    doLast {
        copy {
            from "${buildDir}/resources/main/dependents/platform"
            include "**/**/*.json"
            expand "dependent": "c"
            into "${buildDir}/resources/main/data/c/"
        }
    }
}

// Rename the regular jar to have a distinguishing suffix
//jar {
//    archiveFileName = rootProject.archives_base_name + "-${rootProject.version}-UNSHADED.jar"
//}

shadowJar {
    archiveFileName = rootProject.archives_base_name + "-${rootProject.minecraft_version}-SHADED.jar"
    destinationDirectory = file("${buildDir}/devlibs")
    configurations = [project.configurations.shade]
    relocate 'dependents/platform', 'data/c' // REVIEW not entirely clear on what's going on here
    // embed the epherolib classes and ensure they don't conflict with other versions that might be installed
    from files("../jars/${rootProject.epherolib_jar}")
    relocate 'com/epherical/epherolib', 'refabricated/com/epherical/epherolib'
}

// Configure remapJar to use the shadowJar output
remapJar {
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile
}

//task remappedShadowJar(type: RemapJarTask) {
//    dependsOn tasks.shadowJar
//    input = tasks.shadowJar.archiveFile
//    archiveFileName = rootProject.archives_base_name + "-${rootProject.version}.jar"
//}

//tasks.assemble.dependsOn tasks.remappedShadowJar

//artifacts {
//    archives remappedShadowJar
//}

// https://github.com/modrinth/minotaur
modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: 'MODRINTH_TOKEN_NOT_SET'
    projectId = "${rootProject.modrinth_project_id}"
    versionNumber = "${rootProject.version}"
    versionType = "release"
    uploadFile = remapJar
    changelog = "<p><a href='https://github.com/pcal43/croptopia-refabricated/releases/tag/${rootProject.mod_version}'>https://github.com/pcal43/croptopia-refabricated/releases/tag/${rootProject.mod_version}</a></p>"
    gameVersions = ["${rootProject.minecraft_version}"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
    }
}


// https://github.com/matthewprenger/CurseGradle
curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: 'CURSEFORGE_TOKEN_NOT_SET'

    project {
        id = "${rootProject.curseforge_project_id}"
        releaseType = "release"
        changelog = "https://github.com/pcal43/croptopia-refabricated/releases/tag/${rootProject.mod_version}"
        changelogType = "markdown"
        mod_version = "${rootProject.version}"
        addGameVersion ((String) rootProject.minecraft_version)
        addGameVersion "Fabric"
        mainArtifact(remapJar)
        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}
