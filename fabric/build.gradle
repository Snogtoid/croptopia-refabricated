import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.modrinth.minotaur" version "2.8.7"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    mavenCentral()
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url = 'https://maven.blamejared.com'
    }
    maven {
        url = "https://maven.terraformersmc.com/"
    }
}

archivesBaseName = archives_base_name

configurations {
    shade
    compile.extendsFrom shade
}

sourceSets {
    main {
        java {
            srcDirs += [
                    rootProject.sourceSets.main.java
            ]
        }
        resources {
            srcDirs += [
                    "src/main/generated",
                    rootProject.sourceSets.main.resources
            ]
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    modImplementation files("../jars/${rootProject.epherolib_jar}")
    modCompileOnly "vazkii.patchouli:Patchouli:1.20.1-84-FABRIC-SNAPSHOT",{
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompileOnly "com.terraformersmc:modmenu:${rootProject.modmenu_version}",{
        exclude(group: "net.fabricmc.fabric-api")
    }

    shade(rootProject)
    shade files("../jars/${rootProject.epherolib_jar}")
    compileOnly(rootProject)
    implementation(rootProject)
}

loom {
    accessWidenerPath = file("src/main/resources/croptopia.accesswidener")

    runs {
        client {
            client()
            name "Fabric Client"
            ideConfigGenerated = true
        }

        server {
            server()
            name "Fabric Server"
            ideConfigGenerated = true
        }

        datagen {
            server()
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${getRootProject().file("src/main/generated")}"
            runDir "build/datagen"
            ideConfigGenerated = true
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        // This little horror allows us to have our published modid be 'croptopia-refabricated' while still using datagen to
        // generate resources in the 'croptopia' namespace.  Fabric really doesn't want us to do that, which I guess
        // is sort of understandable, but in our case, given the temporary nature of this codebase, it seems like
        // a reasonable thing to want to do.  This is a little fragile, and it only works because we do runData as a
        // development operation, not as part of the release build.
        def isDatagenBuild = gradle.startParameter.taskNames.contains(":fabric:runData") || gradle.startParameter.taskNames.contains("runData")
        def modId = isDatagenBuild ? "croptopia" : "croptopia-refabricated"
        expand "mod_version": rootProject.version, "mod_id": modId
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }


    //from(sourceSets.main.resources.srcDirs) {
    //    include('**/data/croptopia/recipes/**')
    //    expand "dependent": "c"
    //}

    from(sourceSets.main.resources.srcDirs) {
        include('**/data/**')
        include('**/dependents/**')
        expand "dependent": "c"
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}
tasks.processResources.dependsOn(rootProject.tasks.processResources)

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    source(rootProject.sourceSets.main.allSource)
    doLast {
        copy {
            from "${buildDir}/resources/main/dependents/platform"
            include "**/**/*.json"
            expand "dependent": "c"
            into "${buildDir}/resources/main/data/c/"
        }
    }
}

// Rename the regular jar to have a distinguishing suffix
//jar {
//    archiveFileName = rootProject.archives_base_name + "-${rootProject.version}-UNSHADED.jar"
//}

shadowJar {
    archiveFileName = rootProject.archives_base_name + "-${rootProject.minecraft_version}-SHADED.jar"
    destinationDirectory = file("${buildDir}/devlibs")
    configurations = [project.configurations.shade]
    from files("../jars/${rootProject.epherolib_jar}")
    relocate 'dependents/platform', 'data/c'
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn tasks.shadowJar
    input = tasks.shadowJar.archiveFile
    archiveFileName = rootProject.archives_base_name + "-${rootProject.version}.jar"
}

tasks.assemble.dependsOn tasks.remappedShadowJar

artifacts {
    archives remappedShadowJar
}

// https://github.com/modrinth/minotaur
modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: 'MODRINTH_TOKEN_NOT_SET'
    projectId = "${rootProject.modrinth_project_id}"
    versionNumber = "${rootProject.version}"
    versionType = "release"
    uploadFile = remapJar
    changelog = "<p><a href='https://github.com/pcal43/croptopia-refabricated/releases/tag/${rootProject.mod_version}'>https://github.com/pcal43/croptopia-refabricated/releases/tag/${rootProject.mod_version}</a></p>"
    gameVersions = ["${rootProject.minecraft_version}"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
    }
}


// https://github.com/matthewprenger/CurseGradle
curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: 'CURSEFORGE_TOKEN_NOT_SET'

    project {
        id = "${rootProject.curseforge_project_id}"
        releaseType = "release"
        changelog = "https://github.com/pcal43/croptopia-refabricated/releases/tag/${rootProject.mod_version}"
        changelogType = "markdown"
        mod_version = "${rootProject.version}"
        addGameVersion ((String) rootProject.minecraft_version)
        addGameVersion "Fabric"
        mainArtifact(remapJar)
        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}
